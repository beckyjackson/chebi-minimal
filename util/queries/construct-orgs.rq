PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

CONSTRUCT {
	?newIRI rdfs:subClassOf ?newParent ;
			rdfs:label ?newLabel ;
			owl:equivalentClass [ a owl:Restriction ;
								  owl:onProperty obo:RO_0003001 ;
								  owl:someValuesFrom ?s ] .
}
WHERE {
	?s a owl:Class ;
	   rdfs:label ?label .
	OPTIONAL { ?s rdfs:subClassOf ?parent }
	BIND(IRI(IF(REGEX(STR(?s), "OBI"), 
				REPLACE(STR(?s), "OBI_", "CHEBIsource_"),
				REPLACE(STR(?s),"NCBITaxon_","CHEBIsource_"))) AS ?newIRI)
	BIND(IRI(IF(REGEX(STR(?parent), "OBI"), 
				REPLACE(STR(?parent), "OBI_", "CHEBIsource_"),
				REPLACE(STR(?parent),"NCBITaxon_","CHEBIsource_"))) AS ?newParent)
	BIND(CONCAT("product of ", ?label) AS ?newLabel)
}