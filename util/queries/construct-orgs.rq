PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

CONSTRUCT {
	obo:IEDB_1-ncbitaxon a owl:Class ;
						 rdfs:label "product of organism" .
	?newIRI rdfs:subClassOf ?newParent ;
			rdfs:label ?newLabel ;
			owl:equivalentClass [ a owl:Restriction ;
								  owl:onProperty obo:RO_0003001 ;
								  owl:someValuesFrom ?s ] .
}
WHERE {
	?s rdfs:label ?label ;
	   rdfs:subClassOf ?parent .
	BIND(IRI(CONCAT(REPLACE(STR(?s),"NCBITaxon_","IEDB_"),"-ncbitaxon")) AS ?newIRI)
	BIND(IRI(CONCAT(REPLACE(STR(?parent),"NCBITaxon_","IEDB_"),"-ncbitaxon")) AS ?newParent)
	BIND(CONCAT("product of ", ?label) AS ?newLabel)
}